{ lib }:

let
  inherit (builtins) isList isFunction;
  inherit (lib)
    hasSuffix
    hasPrefix
    mapAttrsFlatten
    ;
  inherit (lib.attrsets)
    attrValues
    mapAttrsRecursiveCond
    mapAttrsToList
    ;
  inherit (lib.lists)
    any
    flatten
    ;
  inherit (lib.fileset)
    fileFilter
    unions
    toSource
    difference
    maybeMissing
    ;

  fileHasAnySuffix =
    fileSuffixes: file: (any (s: hasSuffix s file.name) fileSuffixes);

  mkUnions =
    rootPath: subPaths:
    unions (map (subPath: maybeMissing (rootPath + "/${subPath}")) subPaths);

  toFilesets =
    rootPath: ignoredPaths:
    mapAttrsRecursiveCond (as: !(isList as))
      (
        _: v: mkUnions rootPath v
      )
      ignoredPaths;

  languageIncludeFilesets =
    rootPath:
    toFilesets rootPath {
      common = [
        ".gitignore"
      ];
    };

  languageIgnoreFilesets =
    rootPath:
    toFilesets rootPath {
      common = [
        # Nix build result
        "result"
        # Files generated by IDEs.
        ".idea"
        ".ijwb/"
        ".classpath"
        ".factorypath"
        ".project"
        ".settings"
        ".settings"
        ".vscode"
      ];
      bazel = [
        "bazel.iml"
      ];
      golang = {
        cache = [
          ".go"
          ".gocache"
          ".golang"
        ];
      };
      rust = {
        cache = [
          ".cargo"
          ".cache"
        ];
        build = [
          "target"
          "debug"
        ];
        book = [
          "book"
        ];
      };
      python = {
        venv = [
          ".venv"
        ];
        build = [
          "dist"
          "build"
        ];
        pyTest = [
          ".coverage"
          "htmlcov"
          ".tox"
        ];
        config = [
          ".pypirc"
        ];
      };
    };
  languageFileFilters =
    let
      toFilesets =
        rootPath: ignoreFilters:
        let
          mkFileFilter = condition: fileFilter condition rootPath;
        in
        mapAttrsRecursiveCond (as: !(isFunction as))
          (
            _: v: mkFileFilter v
          )
          ignoreFilters;
    in
    rootPath:
    toFilesets rootPath {
      common = (file: hasSuffix ".swp" file.name);
      bazel = (file: hasPrefix "bazel-" file.name);
      golang = {
        test = (file: hasSuffix ".test" file.name);
        coverage = (file: hasSuffix ".out" file.name);
      };
      rust = {
        logs = (file: hasSuffix ".log" file.name);
        rustfmt = (file: hasSuffix ".rs.bk" file.name);
      };
      python = {
        pyCache = (file: file.name == "__pycache__" || hasSuffix ".pyc" file.name);
        pyTestCache = (
          file: file.name == ".pytest_cache" || hasPrefix ".coverage." file.name
        );
        eggInfo = (file: hasSuffix ".egg-info" file.name);
      };
      secrets = (
        file:
        hasPrefix ".decrypted~" file.name
        || fileHasAnySuffix [ ".agekey" ".pub" ".key" ".pem" ] file
      );
    };
  filterSources =
    { path
    , positiveFileset ? unions [ ]
    , negativeFileset ? unions [ ]
    , extraPositiveFileset ? unions [ ]
    , extraNegativeFileset ? unions [ ]
    ,
    }:
    toSource {
      root = path;
      fileset = unions [
        positiveFileset
        extraPositiveFileset
        (difference
          (unions [
            path
          ])
          (unions [
            negativeFileset
            extraNegativeFileset
          ])
        )
      ];
    };
in
{
  inherit
    filterSources
    languageFileFilters
    languageIgnoreFilesets
    languageIncludeFilesets
    mkUnions
    ;

  filterCommonSources =
    { path
    , extraPositiveFileset ? unions [ ]
    , extraNegativeFileset ? unions [ ]
    ,
    }@attrs:
    let
      ignoreFilesets = languageIgnoreFilesets path;
      ignoreFileFilters = languageFileFilters path;
    in
    filterSources (
      attrs
      // {
        negativeFileset = unions [
          ignoreFilesets.common
          ignoreFileFilters.common
        ];
      }
    );
  filterPythonSources =
    { path
    , extraPositiveFileset ? unions [ ]
    , extraNegativeFileset ? unions [ ]
    ,
    }@attrs:
    let
      ignoreFilesets = languageIgnoreFilesets path;
      ignoreFileFilters = languageFileFilters path;
    in
    filterSources (
      attrs
      // {
        negativeFileset = unions [
          ignoreFilesets.common
          ignoreFilesets.python.venv
          ignoreFilesets.python.build
          ignoreFilesets.python.pyTest
          ignoreFilesets.python.config
          ignoreFileFilters.common
          ignoreFileFilters.python.pyCache
          ignoreFileFilters.python.pyTestCache
          ignoreFileFilters.python.eggInfo
        ];
      }
    );
  filterBazelSources =
    { path
    , extraPositiveFileset ? unions [ ]
    , extraNegativeFileset ? unions [ ]
    ,
    }@attrs:
    let
      ignoreFilesets = languageIgnoreFilesets path;
      ignoreFileFilters = languageFileFilters path;
    in
    filterSources (
      attrs
      // {
        negativeFileset = unions [
          ignoreFilesets.common
          ignoreFilesets.bazel
          ignoreFileFilters.common
          ignoreFileFilters.bazel
        ];
      }
    );
  filterGolangSources =
    { path
    , extraPositiveFileset ? unions [ ]
    , extraNegativeFileset ? unions [ ]
    ,
    }@attrs:
    let
      ignoreFilesets = languageIgnoreFilesets path;
      ignoreFileFilters = languageFileFilters path;
    in
    filterSources (
      attrs
      // {
        negativeFileset = unions [
          ignoreFilesets.common
          ignoreFilesets.golang.cache
          ignoreFileFilters.common
          ignoreFileFilters.golang.test
          ignoreFileFilters.golang.coverage
        ];
      }
    );
  filterRustSources =
    { path
    , extraPositiveFileset ? unions [ ]
    , extraNegativeFileset ? unions [ ]
    ,
    }@attrs:
    let
      ignoreFilesets = languageIgnoreFilesets path;
      ignoreFileFilters = languageFileFilters path;
    in
    filterSources (
      attrs
      // {
        negativeFileset = unions [
          (maybeMissing (path + "/.cargo"))
          ignoreFilesets.common
          ignoreFilesets.rust.cache
          ignoreFilesets.rust.build
          ignoreFilesets.rust.book
          ignoreFileFilters.common
          ignoreFileFilters.rust.logs
          ignoreFileFilters.rust.rustfmt
        ];
      }
    );
}
